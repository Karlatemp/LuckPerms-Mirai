
/*
 * Copyright (c) 2018-2020 Karlatemp. All rights reserved.
 * @author Karlatemp <karlatemp@vip.qq.com> <https://github.com/Karlatemp>
 *
 * LuckPerms-Mirai/LuckPerms-Mirai.public-api/build.gradle
 *
 * Use of this source code is governed by the GNU AFFERO GENERAL PUBLIC LICENSE version 3 license that can be found via the following link.
 *
 * https://github.com/Karlatemp/LuckPerms-Mirai/blob/master/LICENSE
 */

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.jfrog.bintray' version '1.8.5'
    id("maven-publish")
}

dependencies {
    // https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    compileOnly group: 'org.fusesource.jansi', name: 'jansi', version: '1.18'
    compileOnly 'org.spongepowered:configurate-hocon:3.7.1'
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    compileOnly "me.lucko.luckperms:common" // LuckPerms

    compileOnly(project.property('versions.mirai-core-api'))
    compileOnly(project.property('versions.mirai-console'))
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
    testCompile group: 'junit', name: 'junit', version: '4.12'

}

task sourceJar(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.main.allSource)
}
bintray {
    user = project.findProperty("bintray.user") ?: System.getenv("USERNAME")
    println("user is $user")
    key = project.findProperty('bintray.key') ?: System.getenv("TOKEN")

    override = true
    publish = true

    publications = ['mavenPublication']

    pkg {
        repo = 'luckperms'
        name = 'public-api'
        licenses = ['AGPLv3']
        vcsUrl = 'https://github.com/Karlatemp/LuckPerms-Mirai.git'
        version {
            name = project.version
            desc = project.version
            released = new Date()
        }
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from(components.java)
            artifact sourceJar
            pom {
                name = 'LuckPerms Mirai - public api'
                description = 'Public api for LuckPerms'
                url = 'https://github.com/Karlatemp/LuckPerms-Mirai'
            }
            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    true
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}
